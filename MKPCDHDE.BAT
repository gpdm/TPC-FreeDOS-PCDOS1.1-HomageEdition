@ECHO OFF
CLS

REM excluded files, as not available in FreeDOS
REM ART.BAS
REM SAMPLES.BAS
REM MORTGAGE.BAS
REM COLORBAR.BAS
REM CALENDAR.BAS
REM MUSIC.BAS
REM DONKEY.BAS
REM CIRCLE.BAS
REM PIECHART.BAS
REM SPACE.BAS
REM COMM.BAS
REM BALL.BAS
REM BASIC.COM
REM BASICA.COM
REM LINK.EXE


:SELDSKF
CLS
SET DISKFMT=UNKNOWN
ECHO This will create FreeDOS' PC DOS 1.1 Homage Edition.
ECHO.
ECHO Specify disk set to create:
echo 1: 360K
echo 2: 180K
echo 3: 160K
choice /C:123 /n "Make your choice: "
if errorlevel 3 goto D160
if errorlevel 2 goto D180
if errorlevel 1 goto D360
goto SELDSKF


:D360
SET LSTDISK1=FORMAT.EXE SYS.COM CHKDSK.EXE DISKCOPY.EXE DISKCOMP.COM COMP.COM MODE.COM EDLIN.EXE DEBUG.COM EXE2BIN.COM
SET LSTDISK2=
SET LSTDISK3=
SET DISKFMT=360
GOTO DISK1

:D180
SET LSTDISK1=FORMAT.EXE SYS.COM
SET LSTDISK2=CHKDSK.EXE DISKCOPY.EXE DISKCOMP.COM COMP.COM MODE.COM EDLIN.EXE DEBUG.COM EXE2BIN.COM
SET LSTDISK3=
SET DISKFMT=180
GOTO DISK1

:D160
SET LSTDISK1=
SET LSTDISK2=CHKDSK.EXE FORMAT.EXE SYS.COM DISKCOPY.EXE DISKCOMP.COM COMP.COM MODE.COM
SET LSTDISK3=EDLIN.EXE DEBUG.COM EXE2BIN.COM
SET DISKFMT=160
GOTO DISK1


:DISK1
CLS
ECHO Label a new %DISKFMT%k floppy disk as
ECHO '    "FreeDOS PC DOS 1.1 Homage Edition 1.1, Disk 1"'
choice /C:C /N Press 'C' to continue ...

ECHO N | FORMAT A: /S /F:%DISKFMT% /U /V:FDPCD11HE_1

FOR %%F IN (%LSTDISK1%) DO COPY C:\FREEDOS\BIN\%%F A:



:DISK2
CLS
IF "%LSTDISK2%/" == "/" GOTO END
ECHO Label a new %DISKFMT%k floppy disk as
ECHO '    "FreeDOS PC DOS 1.1 Homage Edition 1.1, Disk 2"'
choice /C:C /N Press 'C' to continue ...

ECHO N | FORMAT A: /F:%DISKFMT% /U /V:FDPCD11HE_2

FOR %%F IN (%LSTDISK2%) DO COPY C:\FREEDOS\BIN\%%F A:



:DISK3
CLS
IF "%LSTDISK3%/" == "/" GOTO END
ECHO Label a new %DISKFMT%k floppy disk as
ECHO '    "FreeDOS PC DOS 1.1 Homage Edition 1.1, Disk 3"'
choice /C:C /N Press 'C' to continue ...

ECHO N | FORMAT A: /F:%DISKFMT% /U /V:FDPCD11HE_3

FOR %%F IN (%LSTDISK3%) DO COPY C:\FREEDOS\BIN\%%F A:


:END
ECHO Finished creating FreeDOS' PC-DOS 1.1 Homage Edition
ECHO Enjoy!
